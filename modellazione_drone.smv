MODULE main
VAR
    state : {s1, s2, s3, s4, s5, s6};
    decollo : boolean;
    navigazione : boolean;
    hovering : boolean;
    consegna : boolean;
    posizionamento : boolean;
    atterraggio : boolean;

ASSIGN
    init(state) := s1;
    init(decollo) := TRUE;
    init(navigazione) := FALSE;
    init(hovering) := FALSE;
    init(consegna) := FALSE;
    init(posizionamento) := FALSE;
    init(atterraggio) := FALSE;

    next(state) := 
        case
            state = s1 : s2;  -- da decollo a navigazione
            state = s2 : s3;  -- da navigazione a hovering
            state = s3 : {s2, s4, s5};  -- da hovering a navigazione, consegna o posizionamento
            state = s4 : s3;  -- da consegna a hovering
            state = s5 : s6;  -- da posizionamento a atterraggio
            state = s6 : s1;  -- da atterraggio a decollo
        esac;

    next(decollo) :=
        case
            next(state) = s1 : TRUE;
            TRUE : FALSE;
        esac;

    next(navigazione) :=
        case
            next(state) = s2 : TRUE;     -- se sono in navigazione, navigazione è TRUE
            TRUE : FALSE;
        esac;

    next(hovering) :=
        case
            next(state) = s3 : TRUE;
            TRUE : FALSE;
        esac;

    next(consegna) :=
        case
            next(state) = s4 : TRUE;
            TRUE : FALSE;
        esac;

    next(posizionamento) :=
        case
            next(state) = s5 : TRUE;
            TRUE : FALSE;
        esac;

    next(atterraggio) :=
        case
            next(state) = s6 : TRUE;
            TRUE : FALSE;
        esac;

-- Proprietà LTL
LTLSPEC G(decollo -> X navigazione)
LTLSPEC G(navigazione -> X hovering)
LTLSPEC G(hovering -> X(navigazione | consegna | posizionamento))
LTLSPEC G(consegna -> X hovering)
LTLSPEC G(posizionamento -> X atterraggio)
LTLSPEC G(hovering -> (X consegna -> X X hovering))
LTLSPEC G(atterraggio -> F decollo)

-- Proprietà CTL
CTLSPEC AG(EF decollo)
CTLSPEC AG(decollo -> EF consegna)
CTLSPEC AG !(navigazione & hovering)
CTLSPEC AG(navigazione -> EF hovering)
CTLSPEC AG(hovering -> EF(posizionamento & EX atterraggio))
CTLSPEC AG(hovering -> E[hovering U (consegna | posizionamento)])